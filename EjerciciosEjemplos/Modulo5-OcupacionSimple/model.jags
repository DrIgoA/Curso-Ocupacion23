
model {

# Priors
alpha.psi ~ dnorm(0, 0.01)
beta.psi ~ dnorm(0, 0.01)
alpha.p ~ dnorm(0, 0.01)
beta3.p ~ dnorm(0, 0.01)
beta4.p ~ dnorm(0, 0.01)

# Likelihood
# Modelo Ecologico para el estado real parcialmente observado
for (i in 1:R) {
   z[i] ~ dbern(psi[i])                    # Ocurrencia Real z en sitio i
   psi[i] <- 1 / (1 + exp(-lpsi[i]))
   lpsi[i] <- alpha.psi + beta.psi * edge[i]

   # Modelo de Observacion
   for (j in 1:T) {
      y[i,j] ~ dbern(mu.p[i,j])	# Deteccion-no detection en i j
      mu.p[i,j] <- z[i] * p[i,j]
      p[i,j] <- 1 / (1 + exp(-lp[i,j]))
      lp[i,j] <- alpha.p + beta3.p * HOURS[i,j] + beta4.p * pow(HOURS[i,j], 2)
      } #j
   } #i

# Derived quantities
occ.fs <- sum(z[])                             # Numero de sitios ocupados
mean.p <- exp(alpha.p) / (1 + exp(alpha.p))    # Deteccion media
}

