
model {

# Previas de la comunidad
for(k in 1:M){                  # Loop sobre especies
  lpsi[k] ~ dnorm(mu.lpsi, tau.lpsi)
  lp[k] ~ dnorm(mu.lp, tau.lp)
}

# Hiperprevias de community
omega ~ dunif(0,1)              
mu.lpsi ~ dnorm(0,0.001)        
mu.lp ~ dnorm(0,0.001)          
tau.lpsi <- pow(sd.lpsi, -2)
sd.lpsi ~ dunif(0,5)            
tau.lp <- pow(sd.lp, -2)
sd.lp ~ dunif(0,5)              

# proceso de superpoblacion
for(k in 1:M){
  w[k] ~ dbern(omega)           # indicador de pertenencia a la metacomunidad
}                               

# Modelo ecologico
for(k in 1:M){
  mu.psi[k] <- w[k] * psi[k]    # las especies que no son parte de la comunidad obtienen un cero w
  logit(psi[k]) <- lpsi[k]
  for (i in 1:nsite) {
    z[i,k] ~ dbern(mu.psi[k])
  }
}

# Modelo de obsevacion
for(k in 1:M){
  logit(p[k]) <- lp[k]
  for (i in 1:nsite) {
    mu.p[i,k] <- z[i,k] * p[k]  # las especies que no ocurren tienen 0 en z
    Yaug[i,k] ~ dbin(mu.p[i,k], nrep[i])
  }
}

# Derived quantities
for(k in 1:M){
   Nocc.fs[k] <- sum(z[,k])     # Number of occupied sites among the 267
}
for (i in 1:nsite) {
   Nsite[i] <- sum(z[i,])       # Number of occurring species at each site
}
n0 <- sum(w[(nspec+1):(nspec+nz)]) # Number of unseen species in metacommunity
Ntotal <- sum(w[])              # Total metacommunity size (= nspec + n0)
}

